apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'jacoco'


android {
    namespace = 'org.firstinspires.ftc.teamcode'

    packagingOptions {
        jniLibs.useLegacyPackaging true
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.10"
}

tasks.register("testWithCoverage") {
    dependsOn("testDebugUnitTest", "jacocoTestReport")
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = [
        'jdk.internal.*'
    ]
}

tasks.register("jacocoTestReport", JacocoReport) {
    // You are running debug unit tests
    def variant = "debug"
    def testVariant = "debugUnitTest"

    dependsOn "test${testVariant.capitalize()}"

    reports {
        xml.required = true
        html.required = true
    }

    def allowedClasses = files(
        "$buildDir/intermediates/javac/release/compileReleaseJavaWithJavac/classes/org/firstinspires/ftc/teamcode/TeleOpMain.class",
        "$buildDir/intermediates/javac/release/compileReleaseJavaWithJavac/classes/org/firstinspires/ftc/teamcode/TeleOpMain\$*.class",

        "$buildDir/intermediates/javac/release/compileReleaseJavaWithJavac/classes/org/firstinspires/ftc/teamcode/AutoMain.class",
        "$buildDir/intermediates/javac/release/compileReleaseJavaWithJavac/classes/org/firstinspires/ftc/teamcode/AutoMain\$*.class",

        "$buildDir/intermediates/javac/release/compileReleaseJavaWithJavac/classes/org/firstinspires/ftc/teamcode/Utils_13233"
    )


    def fileFilter = [
        '**/R.class', '**/R$*.class', '**/BuildConfig.*',
        '**/Manifest*.*', 'android/**/*.*',
        '**/org/firstinspires/ftc/teamcode/tuning/**',
        '**/org/firstinspires/ftc/teamcode/**',
    ]


    // Main (non-test) code classes
    def mainClasses = fileTree(
        dir: "$buildDir/intermediates/javac/${variant}/compile${variant.capitalize()}JavaWithJavac/classes",
        excludes: fileFilter
    )

    classDirectories.setFrom(
        files([
            mainClasses,   // everything minus excluded dirs
            allowedClasses // add back specific classes
        ])
    )


    sourceDirectories.setFrom(files([
        "src/main/java"
    ]))

    executionData.setFrom(fileTree(
        dir: buildDir,
        includes: ["jacoco/test${testVariant.capitalize()}.exec"]
    ))
}


repositories {
    maven {
        url = 'https://maven.brott.dev/'
    }
}

dependencies {
    implementation project(':FtcRobotController')

    implementation "com.acmerobotics.roadrunner:ftc:0.1.25"
    implementation "com.acmerobotics.roadrunner:core:1.0.1"
    implementation "com.acmerobotics.roadrunner:actions:1.0.1"
    implementation "com.acmerobotics.dashboard:dashboard:0.5.0"

    // âœ… JUnit 5 for unit testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    testImplementation "org.junit.platform:junit-platform-commons:1.10.0"
    testImplementation "org.mockito:mockito-core:5.10.0"
}
